plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'jacoco'
    id 'org.openapi.generator' version "$openApiGeneratorVersion"
}

group = 'com.gym.crm.microservices'
version = '0.0.1-SNAPSHOT'

application {
    mainClassName = 'com.gym.crm.microservices.trainer.hours.service.TrainerHoursService'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'

    implementation 'org.springframework:spring-orm'
    implementation "org.hibernate:hibernate-core:$hibernateCore"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$openApiVersion"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.24'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: ['org/openapitools/**', 'com/gym/crm/microservices/trainer/hours/service/rest/model'])
                })
        )
    }
}

openApiGenerate {
    inputSpec = "$projectDir/src/main/resources/trainer-hours-api.yaml"
    outputDir = layout.buildDirectory.dir("generated").get().asFile.toString()
    generatorName = "spring"
    modelPackage = "com.gym.crm.microservices.trainer.hours.service.model"
    configOptions = [
            useSpringBoot3: "true",
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
}