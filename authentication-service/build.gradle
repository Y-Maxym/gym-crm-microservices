plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'jacoco'
    id 'org.openapi.generator' version "7.8.0"
}

group = 'com.gym.crm.microservices'
version = '0.0.1-SNAPSHOT'

ext {
    hibernateCore = "6.6.1.Final"
    openApiVersion = "2.6.0"
    jwtVersion = "0.12.6"
    apacheCommons = "1.12.0"
    springCloudVersion = "2023.0.3"
    springBootAdminVersion = "3.3.6"
}

application {
    mainClassName = 'com.gym.crm.microservices.authservice.AuthService'
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    implementation "io.jsonwebtoken:jjwt-jackson:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-impl:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-api:$jwtVersion"
    implementation 'com.auth0:java-jwt:4.4.0'

    implementation "org.apache.commons:commons-text:$apacheCommons"

    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.springframework:spring-orm'
    implementation "org.hibernate:hibernate-core:$hibernateCore"
    implementation 'de.codecentric:spring-boot-admin-starter-client'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$openApiVersion"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.24'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: ['org/openapitools/**', 'com/gym/crm/microservices/authservice/model'])
                })
        )
    }
}

openApiGenerate {
    inputSpec = "$projectDir/src/main/resources/auth-service-api.yml"
    outputDir = layout.buildDirectory.dir("generated").get().asFile.toString()
    generatorName = "spring"
    modelPackage = "com.gym.crm.microservices.authservice.model"
    configOptions = [
            useSpringBoot3: "true",
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        mavenBom "de.codecentric:spring-boot-admin-dependencies:$springBootAdminVersion"
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
}