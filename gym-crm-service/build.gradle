plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'jacoco'
    id 'org.openapi.generator' version "7.8.0"
}

group = 'com.gym.crm.microservices'
version = '0.0.1-SNAPSHOT'

ext {
    hibernateCore = "6.6.1.Final"
    apacheCommons = "1.12.0"
    mapstructVersion = "1.6.2"
    jwtVersion = "0.12.6"
    openApiVersion = "2.6.0"
}

application {
    mainClassName = 'com.gym.crm.app.CrmGymApplication'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    implementation "org.apache.commons:commons-text:$apacheCommons"

    implementation "io.jsonwebtoken:jjwt-jackson:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-impl:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-api:$jwtVersion"
    implementation 'com.auth0:java-jwt:4.4.0'

    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.springframework:spring-orm'
    implementation "org.hibernate:hibernate-core:$hibernateCore"

    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$openApiVersion"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.24'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: ['com/gym/crm/app/facade/mapper/**', 'org/openapitools/**', 'com/gym/crm/app/rest/model'])
                })
        )
    }
}

openApiGenerate {
    inputSpec = "$projectDir/src/main/resources/gym-crm-api.yml"
    outputDir = layout.buildDirectory.dir("generated").get().asFile.toString()
    generatorName = "spring"
    modelPackage = "com.gym.crm.app.rest.model"
    configOptions = [
            useSpringBoot3: "true",
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
}